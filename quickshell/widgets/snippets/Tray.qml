import Quickshell.Services.SystemTray
import QtQuick
import QtQuick.Layouts

Rectangle {
    id: tray

    visible: SystemTray.items.values.length !== 0

    RowLayout {
        id: trayLayout
        anchors.fill: parent
        anchors.centerIn: parent
        anchors.margins: 10
        spacing: 3

        Repeater {
            model: SystemTray.items
            Image {
                id: trayIcon
                required property SystemTrayItem modelData

                source: modelData.icon
                sourceSize.width: 32
                sourceSize.height: 32
                fillMode: Image.PreserveAspectFit

                Loader {
                    id: trayMenuLoader
                    anchors.fill: parent
                    active: true
                    sourceComponent: TrayMenu {
                        id: trayMenu
                        menuHandle: modelData.menu
                        anchorX: trayIcon.x + trayLayout.x + tray.x + leftRow.x + barLayout.x + windowRect.x
                        anchorY: 6
                    }

                    function reloadTrayMenu() {
                        active = !active;
                        active = !active;
                    }
                }

                MouseArea {
                    anchors.fill: parent
                    acceptedButtons: Qt.AllButtons
                    onPressed: event => {
                        if (event.buttons & Qt.LeftButton) {
                            if (modelData.onlyMenu) {
                                trayMenuLoader.item.toggleVisibility();
                            } else {
                                modelData.activate();
                            }
                        }
                        if (event.buttons & Qt.RightButton) {
                            if (modelData.hasMenu) {
                                trayMenuLoader.item.toggleVisibility();
                            } else {
                                modelData.activate();
                            }
                        }
                        if (event.buttons & Qt.MiddleButton) {
                            modelData.secondaryActivate();
                        }
                    }
                }
            }
        }
    }
}
